#version 120
const int nLightCount = 2;
attribute vec4 in_vertex;
attribute vec3 in_normal;
attribute vec2 in_uv;
attribute vec4 in_color;
attribute vec4 in_uv2;

varying vec3 npos, origo;
varying vec3 normal;
varying vec2 uv, uv2, uv3;
varying vec4 pos;
varying vec3 vertMasks;
uniform vec4 wsRot;

varying vec3 light[nLightCount];
varying vec3 lightColor[nLightCount];
varying float dist[nLightCount];

vec3 toLinear(vec3 x) {
	return pow(x, vec3(2.2));
}

float square(float x){
	return x*x;
}

vec3 wsAllign(vec3 x){
	return x + 2.0 * cross(wsRot.xyz, cross(wsRot.xyz, x) + wsRot.w * x);
}
void main()
{
    pos = gl_ModelViewMatrix * in_vertex;
   
    // convert view, normal and light vectors to world space and quaternion correct for model rotation
    mat3 tcamrot = transpose(mat3x3(gl_ModelViewMatrix));
    npos = (wsAllign(normalize(tcamrot * -pos.xyz)));
   
    normal = (tcamrot * (gl_NormalMatrix * wsAllign(normalize(in_normal))));
    for (int i = 0; i < nLightCount; i++) {
        light[i] = wsAllign(normalize((tcamrot * (((gl_LightSource[i].position)).xyz - pos.xyz))));
    }
   
	// store origo for model alligned effects
	origo = in_vertex.xyz;
	
	vertMasks = max(vec3(0.0), in_color.rgb * 3.0 -2.0);	//  shield r, windows g, engine b
	
	uv = in_uv;
	uv.y = 1.0 - uv.y;
	uv2 = in_uv2.xy;
	uv2.y = 1.0 - uv2.y;
	
	uv3 = in_uv2.zw;
	uv3.y = 1.0-uv3.y;
	gl_Position = gl_ProjectionMatrix * pos;
} 
